ENTRY(EntryAsm)
SECTIONS
{
	.low 0x4000 :
	{
		*(.low)
	}
	__KernelStart = 0xC0000000;

	.text __KernelStart + ADDR(.low) + SIZEOF(.low) : AT(LOADADDR(.low) + SIZEOF(.low))
	{
		*(.text)
		*(.text.*)
		*(.rodata)
		*(.rodata.*)
		. = ALIGN(4);
		__CtorsStart = .;
		*(.init_array)
		*(.init_array.*)
		__CtorsEnd = .;
	}
	
	.data : AT(LOADADDR(.text) + SIZEOF(.text))
	{
		*(.data)
		*(.data.*)
	}

	__InitFsStart = .;
	.initfs : AT(LOADADDR(.data) + SIZEOF(.data))
	{
		*(.initfs)
	}
	__InitFsEnd = .;

	.bss (NOLOAD) :
	{
		*(.bss)
		*(COMMON)
		__KernelEnd = ABSOLUTE(.);
	}
}